amends "@pkl.impl.ghactions/PklCI.pkl"

import "@gha/Workflow.pkl"

import "jobs/BuildNativeJob.pkl"
import "jobs/DeployJob.pkl"
import "jobs/GithubRelease.pkl"
import "jobs/GradleJob.pkl"
import "jobs/PklJob.pkl"
import "jobs/SimpleGradleJob.pkl"

triggerDocsBuild = "both"

testReports {
  junit {
    "**/build/test-results/**/*.xml"
  }
  html {
    "**/build/reports/tests/**/*"
  }
}

local gradleCheck: SimpleGradleJob = new {
  isRelease = false
  os = "linux"
  command = "check"
}

local gradleCheckWindows: SimpleGradleJob = new {
  isRelease = false
  os = "windows"
  command = "check"
}

local typealias PklJobs = Mapping<String, PklJob>

local toWorkflowJobs: (PklJobs) -> Workflow.Jobs = (it) -> new Workflow.Jobs {
  for (k, v in it) {
    [k] = v.job
  }
}

local gradleCheckJobs: PklJobs = new {
  ["gradle-check"] = gradleCheck
  ["gradle-check-windows"] = gradleCheckWindows
}

local buildNativeJobs: Mapping<String, BuildNativeJob> = new {
  for (_dist in List("release", "snapshot")) {
    for (_project in List("pkl-cli", "pkl-doc")) {
      for (_arch in List("amd64", "aarch64")) {
        for (_os in List("macOS", "linux")) {
          // TODO re-enable macOS/amd64 builds
          when (!(_os == "macOS" && _arch == "amd64")) {
            ["\(_project)-\(_os)-\(_arch)-\(_dist)"] {
              arch = _arch
              os = _os
              isRelease = _dist == "release"
              project = _project
            }
          }
        }
      }
      ["\(_project)-linux-alpine-amd64-\(_dist)"] {
        arch = "amd64"
        os = "linux"
        musl = true
        isRelease = _dist == "release"
        project = _project
      }
      ["\(_project)-windows-amd64-\(_dist)"] {
        arch = "amd64"
        os = "windows"
        isRelease = _dist == "release"
        project = _project
      }
    }
  }
}

local buildNativeSnapshots = buildNativeJobs.toMap().filter((key, _) -> key.endsWith("snapshot"))

local buildNativeReleases = buildNativeJobs.toMap().filter((key, _) -> key.endsWith("release"))

local benchmarkJob = new SimpleGradleJob { command = "bench:jmh" }

local gradleCompatibilityJob = new SimpleGradleJob {
  command = ":pkl-gradle:build :pkl-gradle:compatibilityTestReleases"
}

local buildAndTestJobs: PklJobs = new {
  ...gradleCheckJobs
  ["bench"] = benchmarkJob
  ["gradle-compatibility"] = gradleCompatibilityJob
  ...buildNativeSnapshots
}

local releaseJobs: PklJobs = new {
  ...gradleCheckJobs
  ["bench"] = benchmarkJob
  ["gradle-compatibility"] = gradleCompatibilityJob
  ...buildNativeReleases
}

// By default, just run ./gradlew check on linux.
// Trigger other checks based on GitHub PR description. Examples:
//
//    * /build windows                     -- Test on Windows
//    * /build native                      -- Test all native builds
//    * /build native-pkl-cli              -- Test all pkl-cli os/arch pairs
//    * /build native-pkl-cli-macos        -- Test pkl-cli on macOS
prb {
  local prbJobs: Mapping<String, GradleJob> = new {
    ["gradle-check"] = gradleCheck
    ["gradle-check-windows"] = (gradleCheckWindows) {
      `if` = "contains(github.event.pull_request.body, '/build windows')"
    }
    for (jobName, job in buildNativeSnapshots) {
      [jobName] = (job) {
        local tags =
          List(
            "native",
            "native-\(job.project)",
            "native-\(job.project)-\(job.os)",
            "native-\(job.project)-\(job.os)-\(job.arch)",
          )
        `if` =
          tags.map((it) -> "contains(github.event.pull_request.body, '/build \(it)')").join(" || ")
      }
    }
  }
  local prbJobs2 = (prbJobs) {
    [[true]] {
      // better SLA when not running on nightly
      nightlyMacOS = false
    }
  }
  jobs = prbJobs2 |> toWorkflowJobs
}

build {
  jobs = buildAndTestJobs |> toWorkflowJobs
}

main {
  jobs =
    (buildAndTestJobs) {
      ["deploy-snapshot"] = (new DeployJob { command = "publishToSonatype" }) {
        needs = buildAndTestJobs.keys.toListing()
      }
    } |> toWorkflowJobs
}

releaseBranch {
  jobs = releaseJobs |> toWorkflowJobs
}

release {
  jobs = (releaseJobs) {
    ["deploy-release"] = (
      new DeployJob { command = "publishToSonatype closeAndReleaseSonatypeStagingRepository" }
    ) {
      needs = releaseJobs.keys.toListing()
    }
    ["github-release"] = (new GithubRelease {}) {
      needs = "deploy-release"
    }
  } |> toWorkflowJobs
}
